# Grammar for the pgen parser specification language
#
# This was used to bootstrap the parser for the emu-grammar parser
# specification language, and it's not clear why we should have two things.
# Ignore this for now.

goal nt grammar {
    token_defs? nt_defs;
}

nt token_defs {
    token_def;
    token_defs token_def;
}

nt token_def {
    "token" IDENT "=" STR ";";
    "var" "token" IDENT ";";
}

nt nt_defs {
    nt_def;
    nt_defs nt_def;
}

nt nt_def {
    "nt" IDENT "{" prods? "}";
    "goal" "nt" IDENT "{" prods? "}";
}

nt prods {
    prod;
    prods prod;
}

nt prod {
    terms ";";
}

nt terms {
    term;
    terms term;
}

nt term {
    symbol;
    symbol "?";
}

nt symbol {
    IDENT;
    STR;
}
